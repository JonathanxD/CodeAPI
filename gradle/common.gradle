import org.jetbrains.kotlin.gradle.tasks.kapt.*

apply plugin: 'idea'

// Programming Language
apply plugin: 'kotlin'
apply plugin: 'java'

// Documentation
apply plugin: 'org.jetbrains.dokka'

apply plugin: 'license'
apply plugin: 'com.github.johnrengelman.shadow'

license {
    header rootProject.file("LICENSE_HEADER")
}

defaultTasks 'licenseFormat', 'build', 'test', 'jar', 'shadowJar'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

// Setup

ext {
    description = "Framework to generate Java code and Bytecode code."
}

shadowJar {
    dependencies {
        include(dependency("com.github.JonathanxD:JwIUtils"))
    }
}

// Tasks
jar {
    from "$rootProject.rootDir/LICENSE"

    manifest {
        attributes 'Implementation-Title': baseName,
                'Implementation-Version': version
    }
}

dokka {
    outputFormat = 'gfm'
    outputDirectory = 'docs'
    reportNotDocumented = false

    linkMapping {
        dir = 'src/main/kotlin'
        url = 'https://github.com/JonathanxD/CodeAPI/tree/version/4.0.0/src/main/kotlin'
        suffix = '#L'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task testJar(type: Jar, dependsOn: testClasses) {
    baseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

configurations {
    tests
}

artifacts {
    archives sourcesJar
    archives javadocJar
    tests testJar
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    compile "com.github.JonathanxD.JwIUtils:JwIUtils:$iutils_version"
    compile "com.github.JonathanxD.JwIUtils:jwiutils-kt:$iutils_version"
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

test {
    useJUnit()
    systemProperty "env", "gradle"
}

license {
    String name = project.getName()
    String description = project.ext.description

    ext.name = "$name"
    ext.organization = 'TheRealBuggy/JonathanxD (https://github.com/JonathanxD/ & https://github.com/TheRealBuggy/)'
    ext.description = "$description"
    ext.url = "https://github.com/JonathanxD/$name"
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.email = 'jonathan.scripter@programmer.net'

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"

    header rootProject.file('LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
        kt = 'SLASHSTAR_STYLE'
    }
}
